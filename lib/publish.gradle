apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

version '1.0.0'
group 'com.bytehamster.lib.searchpreference'

publishing {
    publications {
        Production(MavenPublication) {
            artifact("$buildDir/outputs/aar/lib-release.aar")
            groupId this.group
            artifactId 'searchpreference'
            version this.version

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included in the pom.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

bintray {
    user = BINTRAY_USER
    key = BINTRAY_KEY
    publications = ['Production']
    configurations = ['archives']
    override = true
    pkg {
        repo = 'android'
        name = 'SearchPreference'
        desc = 'Search inside Android Preferences'
        websiteUrl = 'https://github.com/ByteHamster/PreferenceSearch'
        issueTrackerUrl = 'https://github.com/ByteHamster/PreferenceSearch/issues'
        vcsUrl = 'https://github.com/ByteHamster/PreferenceSearch.git'
        licenses = ['MIT']
        publicDownloadNumbers = true
        dryRun = false
        version {
            name = this.version
            desc = "Version ${this.version}"
            released = new Date()
            vcsTag = this.version
        }
    }
}
