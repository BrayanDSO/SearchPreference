apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion 26
    buildToolsVersion '27.0.3'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 26
        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
}

dependencies {
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support:cardview-v7:26.1.0'
}

def releaseVersion = '1.0.1'

publishing {
    publications {
        MyPublication(MavenPublication) {
            groupId 'com.bytehamster.lib.searchpreference'
            artifactId 'searchpreference'
            version releaseVersion

            artifact("$buildDir/outputs/aar/lib-release.aar")
            pom.withXml {
                // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

bintray {
    user = BINTRAY_USER
    key = BINTRAY_KEY
    publications = ['MyPublication']

    dryRun = false
    //publish = true

    pkg {
        repo = 'android'
        name = 'SearchPreference'
        desc = 'Search inside Android Preferences'
        websiteUrl = 'https://github.com/ByteHamster/PreferenceSearch'
        issueTrackerUrl = 'https://github.com/ByteHamster/PreferenceSearch/issues'
        vcsUrl = 'https://github.com/ByteHamster/PreferenceSearch.git'
        licenses = ['MIT']
        version {
            name = releaseVersion
            vcsTag = releaseVersion
            desc = 'Version ' + releaseVersion
            released  = new Date()
            attributes = ['gradle-plugin': 'com.use.less:com.use.less.gradle:gradle-useless-plugin']
        }
    }
}
